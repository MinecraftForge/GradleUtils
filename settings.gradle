pluginManagement {
    repositories {
        //mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0' // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention

    // NOTE: We need to load this into the classpath before GradleUtils for the service to load correctly
    id 'io.freefair.javadoc-links' version '8.14' apply false // https://plugins.gradle.org/plugin/io.freefair.javadoc-links

    id 'net.minecraftforge.gradleutils' version '3.0.0-beta.30' // https://plugins.gradle.org/plugin/net.minecraftforge.gradleutils
    id 'net.minecraftforge.gitversion'  version '3.0.0'         // https://plugins.gradle.org/plugin/net.minecraftforge.gitversion
}

rootProject.name = 'gradleutils'

include 'gradleutils-shared'

dependencyResolutionManagement {
    repositories {
        //mavenLocal()
        mavenCentral()
        maven gradleutils.forgeMaven
        maven { url = 'https://maven.moddinglegacy.com' } // Gradle API
    }

    versionCatalogs {
        register('libs') {
            version 'gradle-javadoc-links', '8.14'

            plugin 'licenser',       'net.minecraftforge.licenser'  version '1.2.0' // https://plugins.gradle.org/plugin/net.minecraftforge.licenser
            plugin 'changelog',      'net.minecraftforge.changelog' version '3.0.0' // https://plugins.gradle.org/plugin/net.minecraftforge.changelog
            plugin 'plugin-publish', 'com.gradle.plugin-publish'    version '1.3.1' // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
            plugin 'shadow',         'com.gradleup.shadow'          version '9.0.2' // https://plugins.gradle.org/plugin/com.gradleup.shadow

            // Static Analysis
            library 'nulls',  'org.jetbrains', 'annotations' version '26.0.2'

            // Gradle API
            // Original: https://github.com/remal-gradle-api/packages/packages/760197?version=9.0.0
            // Mirror:   https://repos.moddinglegacy.com/#/modding-legacy/name/remal/gradle-api/gradle-api/9.0.0
            version 'gradle', '9.0.0'
            library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'

            // GradleUtils Shared Base
            library 'gradleutils-shared', 'net.minecraftforge', 'gradleutils-shared' withoutVersion()

            // JavaDoc Links Plugin
            library 'gradle-javadoc-links', 'io.freefair.javadoc-links', 'io.freefair.javadoc-links.gradle.plugin' versionRef 'gradle-javadoc-links'

            // GitHub Actions Workflows
            library 'yaml', 'org.yaml', 'snakeyaml' version '2.4'
        }

        register('sharedLibs') {
            // Gradle API
            version 'gradle', '8.13'
            library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'

            // Tools
            library 'utils-hash',     'net.minecraftforge', 'hash-utils'      version '0.1.9' // https://files.minecraftforge.net/net/minecraftforge/hash-utils/index.html
            library 'utils-download', 'net.minecraftforge', 'download-utils'  version '0.3.1' // https://files.minecraftforge.net/net/minecraftforge/download-utils/index.html
            bundle 'utils', ['utils-hash', 'utils-download']
        }
    }
}
