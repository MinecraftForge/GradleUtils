plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'gradleutils'

// Applying plugins causes them to not have any IDE support when also applied to any build.gradle files
// The workaround for now is to use this listener here so that it can stay in settings.gradle
// See: https://youtrack.jetbrains.com/issue/IDEA-332061/Gradle-Missing-Code-Completion-Suggestions-for-Settings-Plugins-in-Groovy-DSL
gradle.beforeProject { Project project ->
    project.pluginManager.withPlugin('net.minecraftforge.gradleutils') {
        project.repositories {
            mavenCentral()
            gradlePluginPortal()
            maven project.gradleutils.forgeMaven
            maven { url = 'https://maven.moddinglegacy.com' } // Gradle API
            //mavenLocal()
        }
    }
}

//@formatter:off
dependencyResolutionManagement.versionCatalogs.register('libs') {
    version 'gradle-versions', '0.53.0'

    plugin 'versions',       'com.github.ben-manes.versions'  versionRef 'gradle-versions'
    plugin 'licenser',       'net.minecraftforge.licenser'    version '1.2.0'
    plugin 'gradleutils',    'net.minecraftforge.gradleutils' version '3.3.18'
    plugin 'gitversion',     'net.minecraftforge.gitversion'  version '3.1.1'
    plugin 'changelog',      'net.minecraftforge.changelog'   version '3.1.2'
    plugin 'plugin-publish', 'com.gradle.plugin-publish'      version '2.0.0'
    plugin 'shadow',         'com.gradleup.shadow'            version '9.2.2'

    // Static Analysis
    library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'

    // Gradle API
    // Original: https://github.com/remal-gradle-api/packages/packages/760197?version=7.3
    // Mirror:   https://repos.moddinglegacy.com/#/modding-legacy/name/remal/gradle-api/gradle-api/7.3
    version 'gradle', '7.3'
    library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'

    // Versions Plugin
    library 'gradle-versions', 'com.github.ben-manes.versions', 'com.github.ben-manes.versions.gradle.plugin' versionRef 'gradle-versions'

    // GitHub Actions Workflows
    library 'yaml', 'org.yaml', 'snakeyaml' version '2.4'

    // Git Version
    library 'gitver', 'net.minecraftforge', 'gitversion' version '0.5.2'

    // Backwards compatibility
    library 'jgit', 'org.eclipse.jgit', 'org.eclipse.jgit' version '7.2.0.202503040940-r'
}
//@formatter:on
