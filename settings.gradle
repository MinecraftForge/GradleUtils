plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'gradleutils'

dependencyResolutionManagement {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.moddinglegacy.com' } // Gradle API
        maven { url = 'https://repo.eclipse.org/content/groups/releases/' }
        mavenCentral()
        //mavenLocal()
    }

    versionCatalogs.register('libs') {
        version 'gradle-javadoc-links', '8.14'

        plugin 'licenser',       'net.minecraftforge.licenser'    version '1.2.0'                   // https://plugins.gradle.org/plugin/net.minecraftforge.licenser
        plugin 'gradleutils',    'net.minecraftforge.gradleutils' version '2.6.0'                   // https://plugins.gradle.org/plugin/net.minecraftforge.gradleutils
        plugin 'javadoc-links',  'io.freefair.javadoc-links'      versionRef 'gradle-javadoc-links' // https://plugins.gradle.org/plugin/io.freefair.javadoc-links
        plugin 'plugin-publish', 'com.gradle.plugin-publish'      version '1.3.1'                   // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
        plugin 'shadow',         'com.gradleup.shadow'            version '9.0.0-rc3'               // https://plugins.gradle.org/plugin/com.gradleup.shadow

        // Gradle API
        // TODO [ForgeGradle][FG7][Gradle Api] REMOVE once Gradle publish their own API artifacts
        // Original:  https://github.com/remal-gradle-api/packages/packages/760197?version=9.0.0
        // Mirror:    https://repos.moddinglegacy.com/#/modding-legacy/name/remal/gradle-api/gradle-api/9.0.0
        version 'gradle', '9.0.0'
        library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'
        library 'nulls',  'org.jetbrains',         'annotations' version    '26.0.2'

        // JavaDoc Links Plugin
        library 'gradle-javadoc-links', 'io.freefair.javadoc-links', 'io.freefair.javadoc-links.gradle.plugin' versionRef 'gradle-javadoc-links'

        // GitHub Actions Workflows
        library 'yaml', 'org.yaml', 'snakeyaml' version '2.4'

        // Tools
        library 'utils-hash',     'net.minecraftforge', 'hash-utils'      version '0.1.9' // https://files.minecraftforge.net/net/minecraftforge/hash-utils/index.html
        library 'utils-download', 'net.minecraftforge', 'download-utils'  version '0.3.1' // https://files.minecraftforge.net/net/minecraftforge/download-utils/index.html
        bundle 'utils', ['utils-hash', 'utils-download']
    }
}
