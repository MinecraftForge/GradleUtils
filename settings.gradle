import net.minecraftforge.gradleutils.GradleUtilsExtension

pluginManagement {
    repositories {
        //mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention
    // Java toolchain provisioner to download JDKs for tasks that need them.
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'

    // https://plugins.gradle.org/plugin/io.freefair.javadoc-links
    // Enhanced Javadoc links in projects.
    // GradleUtils 3 adds a service that implements an interface from this plugin.
    // It is added to the classpath here but not yet applied.
    id 'io.freefair.javadoc-links' version '8.14' apply false

    // https://plugins.gradle.org/plugin/net.minecraftforge.gradleutils
    // Gradle utilities for Minecraft Forge projects.
    id 'net.minecraftforge.gradleutils' version '3.0.0-beta.13'
}

// Known bug with Groovy DSL in IntelliJ, it's never getting fixed
// https://youtrack.jetbrains.com/issue/IDEA-332061/Gradle-Missing-Code-Completion-Suggestions-for-Settings-Plugins-in-Groovy-DSL
final gradleutils = extensions.getByType(GradleUtilsExtension)

rootProject.name = 'gradleutils'

dependencyResolutionManagement {
    repositories {
        //mavenLocal()
        mavenCentral()
        maven gradleutils.forgeMaven
        maven { url = 'https://maven.moddinglegacy.com' } // Gradle API
    }

    versionCatalogs.register('libs') {
        version 'gradle-gitversion',    '3.0.0-beta.9'
        version 'gradle-javadoc-links', '8.14'

        plugin 'licenser',       'net.minecraftforge.licenser'    version '1.2.0'                   // https://plugins.gradle.org/plugin/net.minecraftforge.licenser
        plugin 'gitversion',     'net.minecraftforge.gitversion'  versionRef 'gradle-gitversion'    // https://plugins.gradle.org/plugin/net.minecraftforge.gitversion
        plugin 'changelog',      'net.minecraftforge.changelog'   versionRef 'gradle-gitversion'    // https://plugins.gradle.org/plugin/net.minecraftforge.changelog
        plugin 'plugin-publish', 'com.gradle.plugin-publish'      version '1.3.1'                   // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
        plugin 'shadow',         'com.gradleup.shadow'            version '9.0.0-rc3'               // https://plugins.gradle.org/plugin/com.gradleup.shadow

        // Gradle API
        // TODO [ForgeGradle][FG7][Gradle Api] REMOVE once Gradle publish their own API artifacts
        // Original:  https://github.com/remal-gradle-api/packages/packages/760197?version=9.0.0
        // Mirror:    https://repos.moddinglegacy.com/#/modding-legacy/name/remal/gradle-api/gradle-api/9.0.0
        version 'gradle', '9.0.0'
        library 'gradle', 'name.remal.gradle-api', 'gradle-api'  versionRef 'gradle'
        library 'nulls',  'org.jetbrains',         'annotations' version    '26.0.2'
        bundle 'gradle', ['gradle', 'nulls']

        // JavaDoc Links Plugin
        library 'gradle-javadoc-links', 'io.freefair.javadoc-links', 'io.freefair.javadoc-links.gradle.plugin' versionRef 'gradle-javadoc-links'

        // GitHub Actions Workflows
        library 'yaml', 'org.yaml', 'snakeyaml' version '2.4'

        // Tools
        library 'utils-hash',     'net.minecraftforge', 'hash-utils'      version '0.1.9' // https://files.minecraftforge.net/net/minecraftforge/hash-utils/index.html
        library 'utils-download', 'net.minecraftforge', 'download-utils'  version '0.3.1' // https://files.minecraftforge.net/net/minecraftforge/download-utils/index.html
        bundle 'utils', ['utils-hash', 'utils-download']
    }
}
