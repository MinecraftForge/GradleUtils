pluginManagement {
    repositories {
        gradlePluginPortal()

        // GitHub Packages
        maven { url = 'https://maven.moddinglegacy.com/maven' }
    }
}

plugins {
    id 'me.jonathing.gradle.github-packages' version '2.0.0'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.10.0'
}

rootProject.name = 'gradleutils'

dependencyResolutionManagement {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven githubPackage.maven('remal-gradle-api/packages')
        mavenCentral()
    }

    versionCatalogs.register('libs') {
        plugin 'licenser', 'net.minecraftforge.licenser' version '1.2.0'
        plugin 'gradleutils', 'net.minecraftforge.gradleutils' version '2.5.1'
        plugin 'plugin-publish', 'com.gradle.plugin-publish' version '1.3.1'
        plugin 'shadow', 'com.gradleup.shadow' version '9.0.0-beta13'

        // Gradle API
        version 'gradle', '7.3'
        library 'gradle', 'name.remal.gradle-api', 'gradle-api' versionRef 'gradle'
        library 'nulls', 'org.jetbrains', 'annotations' version '26.0.2'

        // GitHub Actions Workflows
        library 'yaml', 'org.yaml', 'snakeyaml' version '2.4'

        // Git Version
        library 'gitver', 'net.minecraftforge', 'gitversion' version '0.5.2'

        // Backwards compatibility
        library 'jgit', 'org.eclipse.jgit', 'org.eclipse.jgit' version '7.2.0.202503040940-r'
    }
}
