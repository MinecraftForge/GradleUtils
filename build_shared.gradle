
java {
    // GitVersion requires Java 17
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://repo.eclipse.org/content/groups/releases/' }
    mavenCentral()
    mavenLocal()
}

dependencies {
    // GitHub Actions Workflows
    implementation libs.yaml

    // Git Version
    implementation libs.gitver

    // TODO - Deprecated git utilities, remove in 3.0
    implementation libs.jgit
}

gradlePlugin {
    website = 'https://github.com/MinecraftForge/GradleUtils'
    vcsUrl = 'https://github.com/MinecraftForge/GradleUtils.git'
    plugins {
        gradleutils {
            id = 'net.minecraftforge.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
            displayName = 'Forge Gradle Utilities'
            description = 'Small collection of utilities for standardizing our gradle scripts'
            tags.set(['minecraftforge'])
        }
        gitversion {
            id = 'net.minecraftforge.gitversion'
            implementationClass = 'net.minecraftforge.gradleutils.gitversion.GitVersionPlugin'
            displayName = 'Git Version'
            description = 'Provides versioning information based on git history and configurable subprojects'
            tags.set(['git', 'version'])
        }
        changelog {
            id = 'net.minecraftforge.changelog'
            implementationClass = 'net.minecraftforge.gradleutils.changelog.ChangelogPlugin'
            displayName = 'Git Changelog'
            description = 'Creates a changelog text file based on git history using GitVersion'
            tags.set(['git', 'changelog'])
        }
    }
}
