import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.gradle.api.attributes.plugin.GradlePluginApiVersion

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.plugin.publish
    alias libs.plugins.shadow
}

final projectDisplayName = 'Forge Gradle Utilities'
description = 'Small collection of utilities for standardizing MinecraftForge gradle scripts'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

// Git Version requires Java 17
java.toolchain.languageVersion = JavaLanguageVersion.of 17

configurations {
    // Applies the "Gradle Plugin API Version" attribute to configuration
    // This was added in Gradle 7, gives consumers useful errors if they are on an old version
    def applyGradleVersionAttribute = { Configuration configuration ->
        configuration.attributes {
            attribute org.gradle.api.attributes.plugin.GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE, objects.named(GradlePluginApiVersion, libs.versions.gradle.get())
        }
    }

    named JavaPlugin.API_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute
    named JavaPlugin.RUNTIME_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute

    named(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME) {
        // Fixes a conflict between Git Version's shadowed SLF4J from JGit and Gradle's own loggers
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}

dependencies {
    // Gradle API
    compileOnly libs.gradle
    compileOnly libs.nulls

    // GitHub Actions Workflows
    implementation libs.yaml

    // Git Version
    implementation libs.gitver

    // Backwards compatibility
    implementation libs.jgit
}

// Removes local Gradle API from compileOnly. This is a workaround for bugged plugins.
// TODO [GradleUtils][GradleAPI] Remove this once they are fixed.
// Publish Plugin: https://github.com/gradle/plugin-portal-requests/issues/260
// Shadow:         https://github.com/GradleUp/shadow/pull/1422
afterEvaluate { project ->
    project.configurations.named(JavaPlugin.COMPILE_ONLY_CONFIGURATION_NAME) { compileOnly ->
        compileOnly.dependencies.remove project.dependencies.gradleApi()
    }
}

license {
    header = rootProject.file 'LICENSE-header.txt'
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    archiveClassifier = 'thin'
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation = true
    archiveClassifier = null
    relocationPrefix = 'net.minecraftforge.gradleutils.shadow'
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.optimizationOptions.indy = true
}

// TODO [GradleUtils][GU3.0] Remove, as normal JavaDocs are used instead, with Groovy classes being hidden impl
// javadocJar is created after evaluation, so we need to configure it here
afterEvaluate {
    tasks.named('javadoc', Javadoc) { enabled = false }
    tasks.named('groovydoc', Groovydoc) { use = true }

    tasks.named('javadocJar', Jar) {
        final groovydoc = tasks.named('groovydoc', Groovydoc).get()

        dependsOn groovydoc
        from groovydoc.destinationDir
    }
}

changelog {
    fromBase()
    publishAll = false
}

gradlePlugin {
    website.set gitversion.url
    vcsUrl.set gitversion.url + '.git'

    plugins {
        register('gradleutils') {
            id = 'net.minecraftforge.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
            displayName = projectDisplayName
            description = project.description
            tags = ['minecraftforge']
        }
        register('changelog') {
            id = 'net.minecraftforge.changelog'
            implementationClass = 'net.minecraftforge.gradleutils.changelog.ChangelogPlugin'
            displayName = 'Git Changelog'
            description = 'Creates a changelog text file based on git history using Git Version'
            tags = ['git', 'changelog']
        }
    }
}

publishing {
    publications.register('pluginMaven', MavenPublication) {
        artifactId = project.name

        changelog.publish it

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            // TODO [GradleUtils][GU3.0] Re-evaluate active developers in GU 3.0
            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.SizableShrimp
                developer gradleutils.pom.developers.Paint_Ninja
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
