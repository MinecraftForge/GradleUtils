import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.ShadowJavaPlugin
import org.gradle.api.attributes.plugin.GradlePluginApiVersion

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'io.freefair.javadoc-links'
    id 'net.minecraftforge.gradleutils'
    id 'net.minecraftforge.gitversion'
    alias libs.plugins.changelog
    alias libs.plugins.licenser
    alias libs.plugins.plugin.publish
    alias libs.plugins.shadow
}

final projectDisplayName = 'Forge Gradle Utilities'
description = 'Small collection of utilities for standardizing MinecraftForge gradle scripts'
base.archivesName = 'gradleutils'
group = 'net.minecraftforge'
// version set by gitversion in settings.gradle

println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
    withJavadocJar()
}

configurations {
    // NOTE: If you are making a new Gradle plugin, do not copy this block!
    configureEach {
        resolutionStrategy.dependencySubstitution {
            // NOTE: We build this in this repo, but enforce the module using a dependency substitution
            //  That way, new plugins can effectively copy this buildscript just without this section
            //  The "shadowed" attribute is declared here since project dependencies do not read the published module file
            substitute module('net.minecraftforge:gradleutils-shared') using variant(project(':gradleutils-shared')) {
                attributes {
                    attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.SHADOWED))
                }
            } because 'Project dependencies do not read the published module file, so the shadowed attribute is manually declared.'
        }
    }

    // Applies the "Gradle Plugin API Version" attribute to configuration
    // This was added in Gradle 7, gives consumers useful errors if they are on an old version
    def applyGradleVersionAttribute = { Configuration configuration ->
        configuration.attributes {
            attribute(GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE, objects.named(GradlePluginApiVersion, libs.versions.gradle.asProvider().get()))
        }
    }

    named(JavaPlugin.RUNTIME_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute)
    named(ShadowJavaPlugin.SHADOW_RUNTIME_ELEMENTS_CONFIGURATION_NAME, applyGradleVersionAttribute)
}

dependencies {
    // Static Analysis
    compileOnly libs.nulls

    // Gradle API
    compileOnly libs.gradle

    // GradleUtils Shared Base
    implementation libs.gradleutils.shared

    // JavaDoc Links Plugin
    compileOnly libs.gradle.javadoc.links

    // GitHub Actions Workflows
    implementation libs.yaml
}

// Removes local Gradle API from compileOnly. This is a workaround for bugged plugins.
// Publish Plugin: https://github.com/gradle/plugin-portal-requests/issues/260
// Shadow:         https://github.com/GradleUp/shadow/pull/1422
afterEvaluate { project ->
    project.configurations.named(JavaPlugin.COMPILE_ONLY_CONFIGURATION_NAME) { compileOnly ->
        compileOnly.dependencies.remove(project.dependencies.gradleApi())
    }
}

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    archiveClassifier = 'thin'
}

tasks.named('shadowJar', ShadowJar) {
    enableAutoRelocation = true
    archiveClassifier = null
    relocationPrefix = 'net.minecraftforge.gradleutils.shadow'
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.optimizationOptions.indy = true
}

tasks.withType(Javadoc).configureEach {
    javadocTool = javaToolchains.javadocToolFor { languageVersion = JavaLanguageVersion.of(24) }

    options { StandardJavadocDocletOptions options ->
        options.windowTitle = projectDisplayName + project.version
        options.tags 'apiNote:a:API Note:', 'implNote:a:Implementation Note:', 'implSpec:a:Implementation Requirements:'
    }
}

changelog {
    from '3.0'
}

gradlePlugin {
    website = gitversion.url
    vcsUrl = gitversion.url + '.git'

    plugins.register('gradleutils') {
        id = 'net.minecraftforge.gradleutils'
        implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
        displayName = projectDisplayName
        description = project.description
        tags = ['minecraftforge']
    }
}

publishing {
    repositories {
        maven gradleutils.publishingForgeMaven
    }

    publications.register('pluginMaven', MavenPublication) {
        changelog.publish(it)

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.addRemoteDetails(pom)

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.Jonathing
                developer gradleutils.pom.developers.Paint_Ninja
                developer gradleutils.pom.developers.LexManos
            }
        }
    }
}

idea.module { downloadSources = downloadJavadoc = true }
eclipse.classpath { downloadSources = downloadJavadoc = true }
