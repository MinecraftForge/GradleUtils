import net.minecraftforge.gradleutils.ChangelogUtils
import net.minecraftforge.gradleutils.GradleUtils
import net.minecraftforge.gradleutils.tasks.ExtractTeamCityProjectConfigurationTask
import net.minecraftforge.gradleutils.tasks.GenerateChangelogTask

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

ext {
    GIT_INFO = GradleUtils.gitInfo(file('.'))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

group 'net.minecraftforge.gradleutils' //Test
version GradleUtils.getTagOffsetVersion(GIT_INFO)
println('Version: ' + version)

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
}

task copyGradleUtilsGroovy(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
        include '**/*.java'
    }
}

task copyGradleUtilsResources(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/resources'
    from('buildSrc/src/main/resources') {
        include '**/*.zip'
    }
}

compileGroovy.dependsOn(copyGradleUtilsResources)
checkLicenseMain.dependsOn(copyGradleUtilsResources)
compileGroovy.dependsOn(copyGradleUtilsGroovy)
checkLicenseMain.dependsOn(copyGradleUtilsGroovy)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

license {
    header = file('HEADER')
    ext {
        name = 'GradleUtils'
        year = 2021
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
}

gradlePlugin {
    plugins {
        gradleutils {
            id = 'net.minecraftforge.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
        }
    }
}

ChangelogUtils.setupChangelogGeneration(project);

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar

            pom {
                name = 'Gradle Utils'
                description = 'Used by MinecraftForge projects as a util library for Gradle buildscripts'
                url = 'https://github.com/MinecraftForge/GradleUtils'
                scm {
                    url = 'https://github.com/MinecraftForge/GradleUtils'
                    connection = 'scm:git:git://github.com/MinecraftForge/GradleUtils.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/GradleUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/GradleUtils/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                    developer {
                        id = 'SizableShrimp'
                        name = 'SizableShrimp'
                    }
                }
            }
        }
    }
    repositories {
        maven GradleUtils.getPublishingForgeMaven(project)
    }
}

task setupTeamCityProject(type: ExtractTeamCityProjectConfigurationTask) {
}

GradleUtils.setupCITasks(project);