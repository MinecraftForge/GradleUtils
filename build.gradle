import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'dev.gradleplugins.gradle-plugin-development' apply false
    id 'java'
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.plugin.publish apply false
    alias libs.plugins.shadow
}

// NOTE: Applying the Plugin Publish plugin causes local Gradle dependencies to be used
// To develop using the target Gradle and Groovy versions, set this to 'false'
final inCI = providers.environmentVariable('CI').<boolean>map({ 'true'.equalsIgnoreCase it }).getOrElse(false)

apply {
    if (!inCI) return
    plugin 'dev.gradleplugins.gradle-plugin-development'
    plugin libs.plugins.plugin.publish.get().pluginId
}

final projectDisplayName = 'Forge Gradle Utilities'
description = 'Small collection of utilities for standardizing MinecraftForge gradle scripts'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

// Git Version requires Java 17
java {
    toolchain.languageVersion = JavaLanguageVersion.of 17
    withSourcesJar()
    withJavadocJar()
}

configurations {
    named(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}

dependencies {
    // Gradle API
    compileOnly gradleApi(libs.versions.gradle.get())
    compileOnly libs.groovy
    compileOnly libs.nulls

    // GitHub Actions Workflows
    implementation libs.yaml

    // Git Version
    implementation libs.gitver

    // Backwards compatibility
    implementation libs.jgit
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
    exclude '**/*.properties'
}

tasks.named('jar', Jar) {
    archiveClassifier = 'thin'
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation = true
    archiveClassifier = null
    relocationPrefix = 'net.minecraftforge.gradleutils.shadow'
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.optimizationOptions.indy = true
}

// TODO [GradleUtils][GU3] Remove, as normal JavaDocs are used instead, with Groovy classes being hidden impl
// javadocJar is created after evaluation, so we need to configure it here
afterEvaluate {
    tasks.named('javadoc', Javadoc) { enabled = false }
    tasks.named('groovydoc', Groovydoc) { use = true }

    tasks.named('javadocJar', Jar) {
        dependsOn groovydoc
        from groovydoc.destinationDir
    }
}

changelog {
    fromBase()
    publishAll = false
}

// weird null-safe ?.tap is in case this extension doesn't exist.
// see start of the buildscript. it's conditionally applied.
extensions.findByType(GradlePluginDevelopmentExtension)?.tap {
    website.set gitversion.url
    vcsUrl.set gitversion.url + '.git'

    plugins {
        register('gradleutils') {
            id = 'net.minecraftforge.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
            displayName = projectDisplayName
            description = project.description
            tags = ['minecraftforge']
        }
        register('changelog') {
            id = 'net.minecraftforge.changelog'
            implementationClass = 'net.minecraftforge.gradleutils.changelog.ChangelogPlugin'
            displayName = 'Git Changelog'
            description = 'Creates a changelog text file based on git history using Git Version'
            tags = ['git', 'changelog']
        }
    }
}

publishing {
    publications.register('pluginMaven', MavenPublication) {
        if (!inCI) from components.java

        artifactId = project.name

        changelog.publish it

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            // TODO [GradleUtils] Re-evaluate active developers in GU 3.0
            developers {
                developer gradleutils.pom.developers.LexManos
                developer gradleutils.pom.developers.SizableShrimp
                developer gradleutils.pom.developers.Paint_Ninja
                developer gradleutils.pom.developers.Jonathing
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadSources = downloadJavadoc = true }
