import net.minecraftforge.gradleutils.ChangelogUtils
import net.minecraftforge.gradleutils.GradleUtils
import net.minecraftforge.gradleutils.tasks.ExtractTeamCityProjectConfigurationTask

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

group 'net.minecraftforge.gradleutils'
version GradleUtils.getTagOffsetVersion(GradleUtils.gitInfo(file('.')))
println "Version: $version"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.register('copyGradleUtilsGroovy', Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
        include '**/*.java'
    }
}

tasks.register('copyGradleUtilsResources', Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/resources'
    from('buildSrc/build/extraresources') {
        include '**/*.zip'
    }
}

tasks.withType(GroovyCompile).configureEach {
    dependsOn 'copyGradleUtilsResources', 'copyGradleUtilsGroovy'
    groovyOptions.optimizationOptions.indy = true
}

tasks.named('checkLicenseMain').configure {
    dependsOn 'copyGradleUtilsResources', 'copyGradleUtilsGroovy'
}

tasks.named('processResources', ProcessResources).configure {
    dependsOn 'copyGradleUtilsResources'
}

tasks.register('sourcesJar', Jar) {
    dependsOn 'copyGradleUtilsResources', 'copyGradleUtilsGroovy'
    archiveClassifier = 'sources'
}

tasks.register('setupTeamCityProject', ExtractTeamCityProjectConfigurationTask) {}

GradleUtils.setupCITasks(project)

artifacts {
    archives tasks.named('sourcesJar', Jar)
}

license {
    header = file('HEADER')
    ext {
        name = 'GradleUtils'
        year = 2023
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
}

gradlePlugin {
    plugins {
        gradleutils {
            id = 'net.minecraftforge.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
        }
    }
}

ChangelogUtils.setupChangelogGenerationFromTag(project, '1.0')

publishing {
    publications.register('pluginMaven', MavenPublication) { mavenPublication ->
        artifact sourcesJar

        ChangelogUtils.setupChangelogGenerationForPublishing(project, mavenPublication)

        pom {
            name = 'Gradle Utils'
            description = 'Used by MinecraftForge projects as a util library for Gradle buildscripts'
            url = 'https://github.com/MinecraftForge/GradleUtils'
            scm {
                url = 'https://github.com/MinecraftForge/GradleUtils'
                connection = 'scm:git:git://github.com/MinecraftForge/GradleUtils.git'
                developerConnection = 'scm:git:git@github.com:MinecraftForge/GradleUtils.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/MinecraftForge/GradleUtils/issues'
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                }
            }

            developers {
                developer {
                    id = 'LexManos'
                    name = 'Lex Manos'
                }
                developer {
                    id = 'SizableShrimp'
                    name = 'SizableShrimp'
                }
            }
        }
    }
    repositories {
        maven GradleUtils.getPublishingForgeMaven(project)
    }
}
